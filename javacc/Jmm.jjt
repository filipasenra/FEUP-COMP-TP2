options
{
    LOOKAHEAD=1;
    MULTI=true;
    STATIC=false;
}

// When in javacc folder
// jjtree Jmm.jjt
// javacc Jmm.jj
// javac *.java
// java Jmm ../bin/test/fixtures/public/Lazysort.jmm

PARSER_BEGIN(Jmm)
public class Jmm {

    private static final int MAX_ERRORS = 10;
    private int nErrors = 0;

    public static void main(String args[]) throws ParseException {
        if(args.length != 1){
            System.err.println("Usage: java Jmm <filename>");
            return;
            }
        
        try {
            Jmm myParser = new Jmm(new java.io.FileInputStream(args[0]));

            SimpleNode root = myParser.ParseExpression(); // returns reference to root node 
            if(myParser.nErrors > 0 ){
                throw new RuntimeException("Has errors");
            }
            root.dump(""); // prints the tree on the screen
            System.out.println("Finished Parsing");

        } 
        catch (java.io.FileNotFoundException e) {
            System.out.println("file " + args[0] + " not found.");
            return;
        }        
    }

    private void errorHandling(ParseException e, int kind) {

        System.out.println("Syntatic error: " + e.toString());  // print the error message
        
        nErrors++;

        if(nErrors > MAX_ERRORS){

            throw new RuntimeException("Reached maximum number of errors (" + MAX_ERRORS + ")!");
            
        }

        error_skipto(kind);

    }

    private void error_skipto(int kind) {
    
        Token t;

        do {
            t = getNextToken();
        
        } while (!(t.kind == kind || t.kind == EOF));
    }
}

PARSER_END(Jmm)


/* Caracters to be skiped */
SKIP:
{
    " " | "\r" | "\t" | "\n" 
    | <"//" (~["\n","\r"])* ("\n"|"\r"|"\r\n")> |         /* Inline comments */
    <"/*" (~["*"])* "*" (~["/"] (~["*"])* "*")* "/">    /* Block comments */
}

/*Reserved Words*/
TOKEN :
{
    <IMPORT: "import" (~[";"])* ";" > //Temporary: Change when professors give all the rules erase
    | <CLASS: "class" >
    | <EXTENDS: "extends">
    | <PUBLIC: "public">
    | <RETURN: "return">
    | <STATIC: "static">
    | <VOID: "void">
    | <MAIN: "main">
    | <STRING: "String">
    | <INT: "int">
    | <BOOLEAN: "boolean">
    | <IF: "if">
    | <ELSE: "else">
    | <WHILE: "while">
    | <LENGTH: "length">
    | <TRUE_: "true">
    | <FALSE_: "false">
    | <THIS: "this">
    | <NEW: "new">
}

/*Identifier and IntegerLiteral*/
TOKEN:
{
    /*IntegerLiteral*/
     < INTEGER_LITERAL : <DECIMAL> | <HEXADECIMAL> | <BINARY> | <OCTAL> > 
   | < #DECIMAL: (["0"-"9"])+ > 
   | < #HEXADECIMAL: ("0x" | "0X")(["0"-"9", "A"-"F", "a"-"f"])+ > 
   | < #BINARY: ("0b" | "0B")(["0", "1"])+ > 
   | < #OCTAL: "0"(["0"-"7"])* > 

   /*Identifier*/
   | < IDENTIFIER : <LETTER> (<LETTER> | <DIGIT>)* > 
   | < #LETTER : ["$", "_", "a"-"z", "A"-"Z"] > 
   | < #DIGIT : ["0"-"9"] >
    
}

/* Delimitaters*/
TOKEN : {
    < SEMICOLON: ";">
    | < COLON: ",">
    | < LEFT_BRACE: "{">
    | < RIGHT_BRACE: "}">
    | < LEFT_BRACKET: "[">
    | < RIGHT_BRACKET: "]">
    | < LEFT_PARENTESIS: "(">
    | < RIGHT_PARENTESIS: ")">
    | < DOT: ".">
}

/*Operators*/
TOKEN : {
    <SUM: "+">
    | <SUB: "-">
    | <MUL: "*">
    | <DIV: "/">
    | <LESS_THAN: "<">
    | <AND: "&&"> 
    | <NEGATION: "!">
    | <ASSIGN: "=">
}

SimpleNode ParseExpression(): {}
{
    ClassDeclaration() <EOF> {return jjtThis;} //Java code inside brackets

}

void ClassDeclaration(): {} {
    (<IMPORT>)*
    <CLASS> <IDENTIFIER> (<EXTENDS> <IDENTIFIER>)? 
    <LEFT_BRACE> 
        ( VarDeclaration() )*
        ( MethodDeclaration() )*
    <RIGHT_BRACE>

}

void VarDeclaration() : {Token t;} {
    Type() t = <IDENTIFIER> <SEMICOLON> {jjtThis.val = t.image;}
}

void Type() #void : {} {
   (<INT> (<LEFT_BRACKET> <RIGHT_BRACKET>)?)
   | <BOOLEAN> | <IDENTIFIER>
}

void MethodDeclaration() : {} {
    <PUBLIC> 
    ((<STATIC> <VOID> <MAIN> 
    <LEFT_PARENTESIS> 
        <STRING> <LEFT_BRACKET> <RIGHT_BRACKET> <IDENTIFIER>
    <RIGHT_PARENTESIS>
    <LEFT_BRACE> 
        (LOOKAHEAD(2) VarDeclaration())* 
        (Statement())*
    <RIGHT_BRACE>)
    |
    (Type() <IDENTIFIER> 
    <LEFT_PARENTESIS> 
        (Type() <IDENTIFIER> (<COLON> Type() <IDENTIFIER>)*)?
    <RIGHT_PARENTESIS>
    <LEFT_BRACE> 
        (LOOKAHEAD(2) VarDeclaration())* 
        (Statement())*
        <RETURN>
        Expression()
        <SEMICOLON>
    <RIGHT_BRACE>))

}


void Statement(): {} {

    (<LEFT_BRACE> (Statement())* <RIGHT_BRACE>)
    | If()
    | While()
    | (LOOKAHEAD(2) Equality() | (Expression() <SEMICOLON>))
}

void If() : {} {
    <IF> 
        <LEFT_PARENTESIS> Expression() <RIGHT_PARENTESIS>
        Statement()
    <ELSE>
        Statement()
}

void While() : {} {
    <WHILE> 
        try {
            <LEFT_PARENTESIS> Expression() <RIGHT_PARENTESIS>

        } catch (ParseException e) {
            
            errorHandling(e, RIGHT_PARENTESIS);
        }
        
        Statement()
}

void Equality() : {Token t;} {
    t = <IDENTIFIER> 
        (<LEFT_BRACKET> Expression() <RIGHT_BRACKET>)? 
        <ASSIGN> Expression() <SEMICOLON> {jjtThis.val = t.image;}
}

void Expression() : {} {

    AND()

}

 void AND() #void : {} {
     LessThan() (LOOKAHEAD(2) <AND> LessThan() #AND(2))*
 }

 void LessThan() #void : {} {
     Sum() (LOOKAHEAD(2) <LESS_THAN> Sum() #LESSTHAN(2))*
 }

 void Sum() #void : {} {
     Mul() (LOOKAHEAD(2) (<SUM> | <SUB>) Mul() #SUM(2))*
 }

 void Mul() #void : {} {
     Literal() (LOOKAHEAD(2) (<MUL> | <DIV>) Literal() #MUL(2))*
 } 

void Literal() #void : {Token t;} {
   DotExpression() | 
   ( t= <INTEGER_LITERAL> | t = <TRUE_> | t = <FALSE_>) {jjtThis.val = t.image;} #Literal
}

void DotExpression() #void : {} {
     BracketExpression() (LOOKAHEAD(2) <DOT> (<IDENTIFIER> | <LENGTH>)
            (<LEFT_PARENTESIS> 
                (Expression() (<COLON> Expression())*)?
            <RIGHT_PARENTESIS>)?
        )*
 } 

 void BracketExpression() #void : {} {
     FinalExpression() (LOOKAHEAD(2) <LEFT_BRACKET> Expression() <RIGHT_BRACKET>)*
 } 

void FinalExpression() #void : {Token t;} {

    New()
    | ( <NEGATION> Expression() #Negation)
    | ( <LEFT_PARENTESIS> Expression() <RIGHT_PARENTESIS>)
    | ( t = <IDENTIFIER> | t = <THIS>) {jjtThis.val = t.image;} #Literal
}

void New() #void : {} {
     <NEW> ( NewIntObject() | NewObject() )
}

void NewIntObject() : {} {
    <INT> <LEFT_BRACKET> Expression() <RIGHT_BRACKET>
}

void NewObject() : {Token t;} {
    t = <IDENTIFIER> <LEFT_PARENTESIS> <RIGHT_PARENTESIS> {jjtThis.val = t.image;}
}